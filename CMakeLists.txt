CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

SET(PROJECT communicator)

PROJECT(${PROJECT})

MACRO(TODAY RESULT)
    IF (WIN32)
        EXECUTE_PROCESS(COMMAND "cmd" " /C date /T" OUTPUT_VARIABLE ${RESULT})
        STRING(REGEX REPLACE "(..).(..).(....).*" "\\1.\\2.\\3" ${RESULT} ${${RESULT}})
    ELSEIF(UNIX)
        EXECUTE_PROCESS(COMMAND "date" "+%d.%m.%Y %H:%M:%S" OUTPUT_VARIABLE ${RESULT})
        STRING(REGEX REPLACE "(..).(..).(....) (..):(..):(..).*" "\\1.\\2.\\3 \\4:\\5:\\6" ${RESULT} ${${RESULT}})
    ELSE (WIN32)
        MESSAGE(SEND_ERROR "date not implemented")
        SET(${RESULT} 000000)
    ENDIF (WIN32)
ENDMACRO(TODAY)

IF (NOT CMAKE_BUILD_TYPE)
    MESSAGE(STATUS "No build type selected, default to Release")
    SET(CMAKE_BUILD_TYPE "Release")
ENDIF()

SET(MAJOR_VERSION 0)
SET(MINOR_VERSION 1)
SET(PATCH_VERSION 0)
TODAY(BUILD_DATE)

SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/Modules/")

SET(Boost_USE_STATIC_LIBS ON)
SET(Boost_USE_MULTITHREADED ON)

FIND_PACKAGE(Oracle)
FIND_PACKAGE(Threads)
FIND_PACKAGE(MongoDB)
FIND_PACKAGE(RabbitMQ)
FIND_PACKAGE(HiRedis)
FIND_PACKAGE(Protobuf REQUIRED)
FIND_PACKAGE(LibConfig REQUIRED)
FIND_PACKAGE(Log4cplus REQUIRED)
FIND_PACKAGE(Boost 1.48 COMPONENTS system thread filesystem regex date_time chrono unit_test_framework serialization REQUIRED)

SET(SOURCES ${SOURCES}
        utils.cpp
        settings.cpp
        server.cpp
        client.cpp
        protocol_base.cpp
    )

SET(TESTS_SOURCES ${TESTS_SOURCES}
        test/params.cpp
    )

IF (${WIN32})
    SET(SOURCES ${SOURCES}
            main_win.cpp
        )
ELSE()
    SET(SOURCES ${SOURCES}
            main_linux.cpp
            daemon.cpp
        )
ENDIF()

INCLUDE(omnicomm/CMakeLists.txt)
INCLUDE(backends/CMakeLists.txt)

INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIR})
INCLUDE_DIRECTORIES(${PROTOBUF_INCLUDE_DIR})
INCLUDE_DIRECTORIES(${LOG4CPLUS_INCLUDE_DIRS})
INCLUDE_DIRECTORIES(${LIBCONFIGPP_INCLUDE_DIR})

IF (ORACLE_FOUND)
    INCLUDE_DIRECTORIES(${ORACLE_INCLUDE_DIRS})
    SET(LIBRARIES ${LIBRARIES} ${ORACLE_LIBRARIES})
    SET(TEST_LIBRARIES ${TEST_LIBRARIES} ${ORACLE_LIBRARIES})
ENDIF(ORACLE_FOUND)

IF (MongoDB_FOUND)
    INCLUDE_DIRECTORIES(${MongoDB_INCLUDE_DIR})
    SET(LIBRARIES ${LIBRARIES} ${MongoDB_LIBRARIES})
    SET(TEST_LIBRARIES ${TEST_LIBRARIES} ${MongoDB_LIBRARIES})
ENDIF(MongoDB_FOUND)

IF (RABBITMQ_FOUND)
    INCLUDE_DIRECTORIES(${RABBITMQ_INCLUDE_DIRS})
    SET(LIBRARIES ${LIBRARIES} ${RABBITMQ_LIBRARIES})
    SET(TEST_LIBRARIES ${TEST_LIBRARIES} ${RABBITMQ_LIBRARIES})
ENDIF(RABBITMQ_FOUND)

IF (CMAKE_COMPILER_IS_GNUCC)
	SET(CMAKE_CXX_FLAGS "-std=c++0x -O2 -Wall -Wno-unused-local-typedefs")
	# SET(CMAKE_CXX_FLAGS "-std=c++0x -g3 -fno-inline -O0 -fbounds-check")
ELSEIF(MSVC)
	SET (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} /EHsc")
ENDIF(CMAKE_COMPILER_IS_GNUCC)

CONFIGURE_FILE(cmake/config.h.in ${CMAKE_SOURCE_DIR}/config.h)

SET(TEST test_${PROJECT})

SET(LIBRARIES ${LIBRARIES}
        ${LIBCONFIGPP_STATIC_LIBRARY}
        ${CMAKE_THREAD_LIBS_INIT}
        ${Boost_LIBRARIES}
        ${PROTOBUF_LIBRARY}
        ${LOG4CPLUS_LIBRARIES}
    )

ADD_EXECUTABLE(${PROJECT} ${SOURCES})
ADD_EXECUTABLE(${TEST} ${TESTS_SOURCES})

TARGET_LINK_LIBRARIES(${PROJECT} ${LIBRARIES})

SET(TEST_LIBRARIES ${TEST_LIBRARIES}
        ${LIBCONFIGPP_STATIC_LIBRARY}
        ${Boost_LIBRARIES}
        ${LOG4CPLUS_LIBRARIES}
    )

TARGET_LINK_LIBRARIES(${TEST} ${TEST_LIBRARIES})

ENABLE_TESTING()
ADD_TEST(${TEST} ${TEST})

SET(CMAKE_INSTALL_PREFIX "${CMAKE_SOURCE_DIR}/bin")
INSTALL(TARGETS communicator DESTINATION .)
INSTALL(FILES log4cplus.conf DESTINATION .)
INSTALL(FILES communicator.conf DESTINATION .)
